buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.marcelofrau.springboot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation('org.springframework.boot:spring-boot-starter-web')
	compile('io.springfox:springfox-swagger2:2.9.2')
	compile('io.springfox:springfox-swagger-ui:2.9.2')

//    compile("org.springframework.boot:spring-boot-starter-security")

//    compile('javax.xml.bind:jaxb-api:2.3.0')
//    compile("com.auth0:java-jwt:3.4.0")

	runtimeOnly('com.h2database:h2')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

task copyJar(type: Copy) {
    dependsOn   'jar'
    from        "build/libs/cities-registry-${project.version}.jar"
    into        'build/docker'
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

task dockerBuild() {
    dependsOn build
    dependsOn copyJar
    doLast {
        exec {
            workingDir '.'
            commandLine "docker", "build", "-t", "marcelofrau/cities-registry", "."
        }
    }
}

task dockerSave() {
    dependsOn dockerBuild
    doLast {
        exec {
            workingDir 'build/docker'
            commandLine 'docker', 'save', '-o', 'cities-registry-image.tar.gz', 'marcelofrau/cities-registry'
        }
    }
}

task dockerRun() {
    dependsOn dockerBuild
    doLast {
        exec {
            workingDir '.'
            commandLine 'docker', 'run', '-p', '8444:8444', 'marcelofrau/cities-registry'
        }
    }
}
