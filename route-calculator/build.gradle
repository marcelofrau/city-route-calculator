buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.marcelofrau.springboot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web')
	compile('io.springfox:springfox-swagger2:2.9.2')
	compile('io.springfox:springfox-swagger-ui:2.9.2')
    compile('org.springframework.cloud:spring-cloud-starter-netflix-hystrix')

	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Finchley.SR2"
    }
}


task copyJar(type: Copy) {
	dependsOn   'jar'
	from        "build/libs/route-calculator-${project.version}.jar"
	into        'build/docker'
	rename { String fileName ->
		fileName.replace("-${project.version}", "")
	}
}

task dockerBuild() {
	dependsOn build
	dependsOn copyJar
	doLast {
		exec {
			workingDir '.'
			commandLine "docker", "build", "-t", "marcelofrau/route-calculator", "."
		}
	}
}

task dockerSave() {
	dependsOn dockerBuild
	doLast {
		exec {
			workingDir 'build/docker'
			commandLine 'docker', 'save', '-o', 'route-calculator-image.tar.gz', 'marcelofrau/route-calculator'
		}
	}
}


task dockerRun() {
	dependsOn dockerBuild
	doLast {
		exec {
			workingDir '.'
			// Please change here to the ip of docker.
			commandLine 'docker', 'run', '--name', 'route-calculator', '-p', '8080:8080', '-e', 'CITIES_REGISTRY_URL=http://192.168.99.100:8081', 'marcelofrau/route-calculator'
		}
	}
}

task dockerStop() {
	doLast {
		exec {
			workingDir '.'
			commandLine 'docker', 'stop', 'route-calculator'
		}
	}
}
